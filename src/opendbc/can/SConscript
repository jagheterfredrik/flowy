Import('env', 'envCython')

import os

# Build Capnp Definitions
msg_capnp = env.Command(
    target=['msg.capnp.c++', 'msg.capnp.h'],
    source=["msg.capnp"],
    action='capnp compile -oc++ $SOURCE'
)

common = ''
envDBC = env.Clone()
dbc_file_path = '-DDBC_FILE_PATH=\'"%s"\'' % (envDBC.Dir("..").abspath)
envDBC['CXXFLAGS'] += [dbc_file_path]
src = ["dbc.cc", "parser.cc", "packer.cc", "common.cc"]
libs = [common, "capnp", "kj", "zmq"]

# shared library for openpilot
libdbc = envDBC.SharedLibrary('libdbc', src, LIBS=libs)

# static library for tools like cabana
envDBC.Library('libdbc_static', src, LIBS=libs)

# Build packer and parser
lenv = envCython.Clone()
parser = lenv.Program('parser_pyx.so', 'parser_pyx.pyx', LIBS=[libdbc, 'python3.11'])
packer = lenv.Program('packer_pyx.so', 'packer_pyx.pyx', LIBS=[libdbc, 'python3.11'])

lenv.Depends(parser, libdbc)
lenv.Depends(packer, libdbc)
